import "classpath:/btdesign/export/metamac-core-common-entity.btdesign"
import "classpath:/btdesign/statistic-sdmx-core-codes.btdesign"
import "classpath:/btdesign/metamac-srm-core-enum.btdesign"
import "classpath:/btdesign/statistic-sdmx-v2_1-domain-enums.btdesign"
import "classpath:/btdesign/metamac-srm-core-base.btdesign"
import "classpath:/btdesign/metamac-srm-core-codes-enum.btdesign"



Application CodesMetamac {
	basePackage=unused

	Module domain_codes_metamac {
		basePackage=org.siemac.metamac.srm.core.code

		// ----------------------------------------------------------------
		// 							SERVICES
		// ----------------------------------------------------------------  
		"Provides access to Codelists Artefacts"
		Service CodesMetamacService {

			> @CodelistVersionMetamacRepository
			> @CodeMetamacRepository
			> @CodelistFamilyRepository

			// CODELISTS

			"Creates codelist"
			@CodelistVersionMetamac createCodelist(@CodelistVersionMetamac codelistVersion) throws MetamacException;
			"Updates codelist"
			@CodelistVersionMetamac updateCodelist(@CodelistVersionMetamac codelistVersion) throws MetamacException;
			"Retrieves codelist by urn"
			@CodelistVersionMetamac retrieveCodelistByUrn(String urn) throws MetamacException;
			"Retrieves all codelists versions"
			List<@CodelistVersionMetamac> retrieveCodelistVersions(String urn) throws MetamacException;
			"Finds codelists by criteria"
    		PagedResult<@CodelistVersionMetamac> findCodelistsByCondition(List<ConditionalCriteria> conditions, PagingParameter pagingParameter)  throws MetamacException;
    		"Sends codelist to production validation"
    		@CodelistVersionMetamac sendCodelistToProductionValidation(String urn) throws MetamacException;
    		"Sends codelist to diffusion validation"
    		@CodelistVersionMetamac sendCodelistToDiffusionValidation(String urn) throws MetamacException;
	    	"Rejects production validation of codelist"    		
	    	@CodelistVersionMetamac rejectCodelistProductionValidation(String urn) throws MetamacException;
	    	"Rejects diffusion validation of codelist"
	    	@CodelistVersionMetamac rejectCodelistDiffusionValidation(String urn) throws MetamacException;
	    	"Publish internally a codelist"
	    	@CodelistVersionMetamac publishInternallyCodelist(String urn) throws MetamacException;
	    	"Publish externally a codelist"
	    	@CodelistVersionMetamac publishExternallyCodelist(String urn) throws MetamacException;
			"Deletes codelist version. This must be last version and no published"
    		deleteCodelist(String urn) throws MetamacException;
    		"Creates a new version of a codelist, copying metadata from version with provided urn"
    		@CodelistVersionMetamac versioningCodelist(String urnToCopy, @VersionTypeEnum versionType) throws MetamacException;
    		"Ends the codelist validity"
    		@CodelistVersionMetamac endCodelistValidity(String urn) throws MetamacException;
    		"Retrieves codelist by code urn"
    		@CodelistVersionMetamac retrieveCodelistByCodeUrn(String codeUrn) throws MetamacException;

			// CODES

			"Creates code"
			@CodeMetamac createCode(String codelistUrn, @CodeMetamac code) throws MetamacException;
			"Updates code"
			@CodeMetamac updateCode(@CodeMetamac code) throws MetamacException;
    		"Retrieves code by urn"
    		@CodeMetamac retrieveCodeByUrn(String urn) throws MetamacException;
    		"Finds codes by criteria"
    		PagedResult<@CodeMetamac> findCodesByCondition(List<ConditionalCriteria> conditions, PagingParameter pagingParameter)  throws MetamacException;
			"Deletes code"
    		deleteCode(String urn) throws MetamacException;
    		"Retrieves codes by codelist, in tree structure"
    		List<@CodeMetamac> retrieveCodesByCodelistUrn(String codelistUrn) throws MetamacException;

			// CODESLIST FAMILIES
			
			"Creates codelist family"
			@CodelistFamily createCodelistFamily(@CodelistFamily codelistFamily) throws MetamacException;
			"Updates codelist family"
			@CodelistFamily updateCodelistFamily(@CodelistFamily codelistFamily) throws MetamacException;
    		"Retrieves codelist family by urn"
    		@CodelistFamily retrieveCodelistFamilyByIdentifier(String identifier) throws MetamacException;
    		"Finds codelist families by criteria"
    		PagedResult<@CodelistFamily> findCodelistFamiliesByCondition(List<ConditionalCriteria> conditions, PagingParameter pagingParameter)  throws MetamacException;
			"Deletes code"
    		deleteCodelistFamily(String identifier) throws MetamacException;
		}

		// ----------------------------------------------------------------
		// 							ENTITIES
		// ----------------------------------------------------------------

		Entity CodelistVersionMetamac extends @CodelistVersion {
			databaseTable="TB_M_CODELISTS_VERSIONS"
			hint="idSequence=M_CODELISTS_VERSIONS"
			
			"Short name"
			- @InternationalString shortName nullable cascade="all" databaseColumn="SHORT_NAME_FK";
			"Is recommended"
			Boolean isRecommended nullable;
			"Access type"
			- @AccessTypeEnum accessType nullable;
			"Family"
			- @CodelistFamily family cascade="none" nullable databaseColumn="CODELIST_FAMILY_FK" <-> codelists;
			
			"Life cycle information"
			- @SrmLifeCycleMetadata lifeCycleMetadata databaseColumn="";

			Repository CodelistVersionMetamacRepository {
				findById;
				findByQuery(String query, Map<String, Object> parameters, int maxResult);
				findByCondition(PagingParameter pagingParameter);
				@CodelistVersionMetamac findByUrn(String urn);
				@CodelistVersionMetamac retrieveCodelistVersionByProcStatus(String urn, ArrayProcStatusEnum procStatus) throws MetamacException;
				@CodelistVersionMetamac findByCode(String urn);
			}			
		}

		Entity CodeMetamac extends @Code {
			databaseTable="TB_M_CODES"
			hint="idSequence=M_CODES"
			
			Repository CodeMetamacRepository {
				findById;
				findByCondition(PagingParameter pagingParameter);
			}
		}

		Entity CodelistFamily {
			databaseTable="TB_M_CODELIST_FAMILIES"
			hint="idSequence=M_CODELIST_FAMILIES"
			
			"Identifier"
			String identifier nullable;
			"Name"
			- @InternationalString name nullable cascade="all" databaseColumn="NAME_FK";
			"Codelists"
			- Bag<@CodelistVersionMetamac> codelists cascade="none" fetch="lazy" inverse <-> family;
			
			Repository CodelistFamilyRepository {
				findById;
				save;
				delete;
				findByQuery(String query, Map<String, Object> parameters, int maxResult);
				findByCondition(PagingParameter pagingParameter);
				@CodelistFamily findByIdentifier(String identifier);
			}
		}

	}
}