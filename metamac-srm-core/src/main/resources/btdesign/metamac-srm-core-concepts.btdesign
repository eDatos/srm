import "classpath:/btdesign/export/metamac-core-common-entity.btdesign"
import "classpath:/btdesign/statistic-sdmx-core-concepts.btdesign"
import "classpath:/btdesign/metamac-srm-core-enum.btdesign"
import "classpath:/btdesign/metamac-srm-core-concepts-enum.btdesign"
import "classpath:/btdesign/statistic-sdmx-v2_1-domain-enums.btdesign"

Application ConceptsMetamac {
	basePackage=unused

	Module domain_concepts_metamac {
		basePackage=org.siemac.metamac.srm.core.concept

		// ----------------------------------------------------------------
		// 							SERVICES
		// ----------------------------------------------------------------  
		"Provides access to Scheme Concepts Artefacts"
		Service ConceptsMetamacService {
			
			> @ConceptSchemeVersionMetamacRepository
			
			"Create concept scheme"
			@ConceptSchemeVersionMetamac createConceptScheme(@ConceptSchemeVersionMetamac conceptSchemeVersion) throws MetamacException;
			"Update concept scheme"
			@ConceptSchemeVersionMetamac updateConceptScheme(@ConceptSchemeVersionMetamac conceptSchemeVersion) throws MetamacException;
			"Retrieve concept scheme by urn"
			@ConceptSchemeVersionMetamac retrieveConceptSchemeByUrn(String urn) throws MetamacException;
    		"Finds concept schemes by criteria in any status, returning last versions"
    		PagedResult<@ConceptSchemeVersionMetamac> findConceptSchemesByCondition(List<ConditionalCriteria> conditions, PagingParameter pagingParameter)  throws MetamacException;
    		"Send concept scheme to production validation"
    		@ConceptSchemeVersionMetamac sendConceptSchemeToProductionValidation(String urn) throws MetamacException;
    		"Send concept scheme to diffusion validation"
    		@ConceptSchemeVersionMetamac sendConceptSchemeToDiffusionValidation(String urn) throws MetamacException;
	    	"Rejects production validation of concept scheme"    		
	    	@ConceptSchemeVersionMetamac rejectConceptSchemeProductionValidation(String urn) throws MetamacException;
	    	"Rejects diffusion validation of concept scheme"
	    	@ConceptSchemeVersionMetamac rejectConceptSchemeDiffusionValidation(String urn) throws MetamacException;
	    	"Publish internally a concept scheme"
	    	@ConceptSchemeVersionMetamac publishInternallyConceptScheme(String urn) throws MetamacException;
	    	"Publish externally a concept scheme"
	    	@ConceptSchemeVersionMetamac publishExternallyConceptScheme(String urn) throws MetamacException;
			"Delete concept scheme version. This must be last version and no published"
    		deleteConceptScheme(String urn) throws MetamacException;
    		"Creates a new version of a concept scheme"
    		@ConceptSchemeVersionMetamac versioningConceptScheme(String urn, @VersionTypeEnum versionType) throws MetamacException;
		}

		// ----------------------------------------------------------------
		// 							ENTITIES
		// ----------------------------------------------------------------  
		Entity ConceptSchemeVersionMetamac extends @ConceptSchemeVersion {
			databaseTable="TB_M_CONCEPT_SCHEMES_VERSIONS"
			hint="idSequence=M_CONCEPT_SCHEMES_VERSIONS"
			!auditable

			"Status"
			- @ItemSchemeMetamacProcStatusEnum procStatus;
			"Type"
			- @ConceptSchemeTypeEnum type databaseColumn="CONCEPT_SCHEME_TYPE";
			"Related operation, only when type is Operation"
			- @ExternalItem relatedOperation nullable cascade="all" databaseColumn="RELATED_OPERATION_FK";
			
			"Date when the concept scheme was sended to production validation"
			DateTimeTZ productionValidationDate nullable;
			"User who sended to production validation"
			String productionValidationUser nullable;
			"Date when the concept scheme was sended to diffusion validation"
			DateTimeTZ diffusionValidationDate nullable;
			"User who sended to diffusion validation"
			String diffusionValidationUser nullable;
			"Date when the concept scheme was published internally"
			DateTimeTZ internalPublicationDate nullable;
			"User who published internally"
			String internalPublicationUser nullable;
			"Date when the concept scheme was published externally"
			DateTimeTZ externalPublicationDate nullable;
			"User who published externally"
			String externalPublicationUser nullable;
			
			"Last update to optimistic locking"
			DateTimeTZ updateDateMetamac nullable;
			
			Repository ConceptSchemeVersionMetamacRepository {
				findById;
				findByCondition(PagingParameter pagingParameter);
			}			
		}
	}
}