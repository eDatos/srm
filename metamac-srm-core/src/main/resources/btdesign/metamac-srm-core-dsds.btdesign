import "classpath:/btdesign/statistic-sdmx-core-base.btdesign"
import "classpath:/btdesign/statistic-sdmx-core-structure.btdesign"
import "classpath:/btdesign/metamac-srm-core-enum.btdesign"
import "classpath:/btdesign/statistic-sdmx-v2_1-domain-enums.btdesign"
import "classpath:/btdesign/metamac-srm-core-base.btdesign"
import "classpath:/btdesign/metamac-core-common-enum.btdesign"
import "classpath:/btdesign/export/metamac-core-common-entity.btdesign"
import "classpath:/btdesign/metamac-srm-core-concepts.btdesign"

Application DsdsMetamac {
	basePackage=unused

	Module domain_dsds_metamac {
		basePackage=org.siemac.metamac.srm.core.dsd

		// ----------------------------------------------------------------
		// 							SERVICES
		// ----------------------------------------------------------------  
		"Provides access to Dsd Artefacts"
		Service DsdsMetamacService {
			
			> @DataStructureDefinitionVersionMetamacRepository
    		
    		"Creates DataStructureDefinition"
			@DataStructureDefinitionVersionMetamac createDataStructureDefinition(@DataStructureDefinitionVersionMetamac dataStructureDefinitionVersion) throws MetamacException;
			"Pre-Create DataStructureDefinition"
			@DataStructureDefinitionVersionMetamac preCreateDataStructureDefinition(@DataStructureDefinitionVersionMetamac dataStructureDefinitionVersion) throws MetamacException;
			"Updates DataStructureDefinition"
			@DataStructureDefinitionVersionMetamac updateDataStructureDefinition(@DataStructureDefinitionVersionMetamac dataStructureDefinitionVersion) throws MetamacException;
    		"Retrieves data structure definition by urn"
			@DataStructureDefinitionVersionMetamac retrieveDataStructureDefinitionByUrn(String urn) throws MetamacException;
			"Retrieves all data structure definition versions"
			List<@DataStructureDefinitionVersionMetamac> retrieveDataStructureDefinitionVersions(String urn) throws MetamacException;
			"Finds data structures by criteria"
    		PagedResult<@DataStructureDefinitionVersionMetamac> findDataStructureDefinitionsByCondition(List<ConditionalCriteria> conditions, PagingParameter pagingParameter) throws MetamacException;
          	
          	@ComponentList saveDescriptorForDataStructureDefinition(String dataStructureDefinitionVersionUrn, @ComponentList componentList) throws MetamacException;
	   		deleteDescriptorForDataStructureDefinition(String dataStructureDefinitionVersionUrn, @ComponentList componentList) throws MetamacException;
	   
	   		@Component saveComponentForDataStructureDefinition(String dataStructureDefinitionVersionUrn, @Component component) throws MetamacException;	   
	    	deleteComponentForDataStructureDefinition(String dataStructureDefinitionVersionUrn, @Component component) throws MetamacException;
          	
          	
			"Sends data structure definition to production validation"
    		@DataStructureDefinitionVersionMetamac sendDataStructureDefinitionToProductionValidation(String urn) throws MetamacException;
    		"Sends data structure definition to diffusion validation"
    		@DataStructureDefinitionVersionMetamac sendDataStructureDefinitionToDiffusionValidation(String urn) throws MetamacException;
	    	"Rejects production validation of data structure definition"    		
	    	@DataStructureDefinitionVersionMetamac rejectDataStructureDefinitionProductionValidation(String urn) throws MetamacException;
	    	"Rejects diffusion validation of data structure definition"
	    	@DataStructureDefinitionVersionMetamac rejectDataStructureDefinitionDiffusionValidation(String urn) throws MetamacException;
	    	"Publish internally a data structure definition"
	    	@DataStructureDefinitionVersionMetamac publishInternallyDataStructureDefinition(String urn) throws MetamacException;
	    	"Publish externally a data structure definition"
	    	@DataStructureDefinitionVersionMetamac publishExternallyDataStructureDefinition(String urn) throws MetamacException;
			"Deletes data structure definition version. This must be last version and no published"
    		deleteDataStructureDefinition(String urn) throws MetamacException;
    		"Creates a new version of a data structure definition, copying metadata from version with provided urn"
    		@DataStructureDefinitionVersionMetamac versioningDataStructureDefinition(String urnToCopy, @VersionTypeEnum versionType) throws MetamacException;
    		"Ends the data structure definition validity"
    		@DataStructureDefinitionVersionMetamac endDataStructureDefinitionValidity(String urn) throws MetamacException;
   		
    		"Find concept schemes with concepts can be primary measure"
    		PagedResult<@ConceptSchemeVersionMetamac> findConceptSchemesWithConceptsCanBeDsdPrimaryMeasureByCondition(List<ConditionalCriteria> conditions, PagingParameter pagingParameter, String dsdUrn) throws MetamacException;
    		"Find concepts can be primary measure"
    		PagedResult<@ConceptMetamac> findConceptsCanBeDsdPrimaryMeasureByCondition(List<ConditionalCriteria> conditions, PagingParameter pagingParameter, String dsdUrn) throws MetamacException;
   		
		}

		// ----------------------------------------------------------------
		// 							ENTITIES
		// ----------------------------------------------------------------  
		Entity DataStructureDefinitionVersionMetamac extends @DataStructureDefinitionVersion {
			databaseTable="TB_M_DATASTRUCTURE_VERSIONS"
			hint="idSequence=M_DATASTRUCTURE_VERSIONS"
			!auditable

			"Life cycle information"
			- @SrmLifeCycleMetadata lifeCycleMetadata databaseColumn="";
			
			"Visualisation metadata Auto Open"
			Boolean autoOpen nullable;
			
			"Visualisation metadata Show Decimals"
			Integer showDecimals nullable range="min=0,max=6,message='exception.srm.dsd.validation.showdecimals_precision'";
			
			"Visualisation metadata Heading, dimensions list ordered"
			- Bag<@DimensionOrder> headingDimensions cascade="all-delete-orphan" fetch="lazy" inverse orderby="dimOrder asc" <-> dsdVersionHeading;
			
			"Visualisation metadata Stub, dimensions list ordered"
			- Bag<@DimensionOrder> stubDimensions cascade="all-delete-orphan" fetch="lazy" inverse orderby="dimOrder asc" <-> dsdVersionStub;
			
			"Visualisation metadata Stub, dimensions list ordered"
			- Bag<@MeasureDimensionPrecision> showDecimalsPrecisions fetch="lazy" cascade="all-delete-orphan" inverse orderby="id asc" <-> dsdVersion;
			
			"Statistical operation"
			- @ExternalItem statisticalOperation nullable cascade="all" databaseColumn="STATISTICAL_OPERATION_FK";
			
			Repository DataStructureDefinitionVersionMetamacRepository {
				findByQuery(String query, Map<String, Object> parameters, int maxResult);
				findByCondition(PagingParameter pagingParameter);
				@DataStructureDefinitionVersionMetamac findByUrn(String urn);
				@DataStructureDefinitionVersionMetamac retrieveDataStructureDefinitionVersionByProcStatus(String urn, ArrayProcStatusEnum procStatusArray) throws MetamacException;
			}			
		}
		
		Entity DimensionOrder {
			databaseTable="TB_M_DIMENSION_ORDERS"
			hint="idSequence=M_DIMENSION_ORDERS"
			!auditable
			
			// Order
			Integer dimOrder not nullable;
			
			"DSD dimension"
			-@DimensionComponent dimension cascade="none" databaseColumn="DIM_COMPONENT_FK";
			
			"Not null when dimension is in heading"
			- @DataStructureDefinitionVersionMetamac dsdVersionHeading nullable cascade="none" fetch="lazy" databaseColumn="DSD_VERSION_HEADING_FK" <-> headingDimensions;
			
			"Not null when dimension is in stub"
			- @DataStructureDefinitionVersionMetamac dsdVersionStub nullable cascade="none" fetch="lazy" databaseColumn="DSD_VERSION_STUB_FK" <-> stubDimensions;
		}
		
		Entity MeasureDimensionPrecision {
			databaseTable="TB_M_MEASURE_DIM_PRECISIONS"
			hint="idSequence=M_MEASURE_DIM_PRECISIONS"
			!auditable

			"Code of Concept representation associate to Measure Dimension"
			-@ConceptMetamac concept cascade="none" databaseColumn="M_CONCEPT_FK";
			
			"Show Decimal Precision, must a value between 0 and 6"
			Integer showDecimalPrecision range="min=0,max=6,message='exception.srm.dsd.validation.showdecimals_precision'";
			
			"DSD version"
			- @DataStructureDefinitionVersionMetamac dsdVersion not nullable cascade="none" fetch="lazy" databaseColumn="DSD_VERSION_FK" <-> showDecimalsPrecisions;
		}

	}
}